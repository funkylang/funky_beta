#
  Copyright (C) 2025 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License, version 2, or
  (at your option) version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files GPLv2.txt and GLPv3.txt or
  http://www.gnu.org/licenses/gpl-2.0.html
  http://www.gnu.org/licenses/gpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

<using std-2.0>
<using tedi>

$is_a_keyword
  hash_set
    "auto"
    "break"
    "case"
    "char"
    "const"
    "continue"
    "default"
    "do"
    "double"
    "else"
    "enum"
    "extern"
    "float"
    "for"
    "goto"
    "if"
    "int"
    "long"
    "register"
    "return"
    "short"
    "signed"
    "sizeof"
    "static"
    "struct"
    "switch"
    "typedef"
    "union"
    "unsigned"
    "void"
    "volatile"
    "while"
    "if"
    "else"
    "return"

$tedi::create_c_line: (template_screen line)
  $indent indent_of(line)
  $text text_of(line)
  $screen create_screen(template_screen indent+width_of(text) 1)
  $x indent+1
  if
    text(1) == '#':
      set_draw_colour &screen '@0xc06000;'
      set_text_attributes &screen BOLD|ITALIC
      draw_text screen x 1 text
    :
      $i 1
      $n length_of(text)
      loop:
	if
	  i > n
	  -> screen
	  :
	    $chr text(i)
	    case chr
	      '{', '}':
		set_text_attributes &screen BOLD
		draw_character &screen x 1 chr
		inc &x
		inc &i
		next
	      'a' .. 'z', 'A' .. 'Z':
		$s i
		inc &i
		loop
		  :
		    if
		      i > n
		      add_identifier
		      :
			if
			  text(i).is_an_identifier_character:
			    inc &i
			    next
			  add_identifier

		    $add_identifier:
		      $symbol range(text s i-1)
		      if
			is_a_keyword(symbol):
			  set_text_attributes &screen BOLD
			  draw_symbol
			draw_symbol

		      $draw_symbol:
			draw_text &screen x 1 symbol
			plus &x width_of(symbol)
			break
		  next
	      :
		set_text_attributes &screen NORMAL
		draw_character &screen x 1 chr
		plus &x width_of(chr)
		inc &i
		next

      $is_an_identifier_character: (chr)
	-> chr.is_a_letter || chr.is_a_digit || chr == '_'
