#
  Copyright (C) 2024 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU Library General Public License, version 2, or
  (at your option) under the terms of the GNU Lesser General Public License,
  version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU Lesser (Library) General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files LGPLv2.txt and LGLPv3.txt or
  http://www.gnu.org/licenses/lgpl-2.0.html
  http://www.gnu.org/licenses/lgpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

#
  Topic: Authentication

# Kinds

$std::RANDOM_BASED_UUID .
$std::TIME_BASED_UUID .
$std::NAME_BASED_UUID .

$std::generate_uuid ()

$std_types::object/generate_uuid generate_id
$std_types::undefined/generate_uuid generate_id

$generate_id:
  #
    generates and returns a Universally Unique Identifier (UUID)
  (
    _kind # how to create the UUID
  )
  load! $rnd "/dev/urandom" 16
  current_time! $t
  -> compute_uuid(t rnd)

$std_types::io/generate_uuid:
  #
    creates and returs a Universally Unique Identifier (UUID)
  (
    io
    id
    _kind # how to create the UUID
  )
  tuple &id 1
  load &io id "/dev/urandom" 16
  register_handlers io id
    JOB_COMPLETED = load_completed
    JOB_FAILED = load_failed

$load_completed: (io id rnd)
  deregister_all_handlers &io id
  id !id
  -> io tuple(JOB_COMPLETED id compute_uuid(current_time_of(io) rnd))

$load_failed: (io id err)
  deregister_all_handlers &io id
  id !id
  -> io tuple(JOB_FAILED id err)

$compute_uuid: (t rnd)
  to_uint8_array &rnd
  !t
    to_uint8_array
      range
	pad_right
	  append(seconds_since_epoch(t).to_string second_of(t).to_string)
	  16
	  "0"
	-16
	-1
  $uuid rnd^t
  ->
    append
      hex(uuid(1) 2)
      hex(uuid(2) 2)
      hex(uuid(3) 2)
      hex(uuid(4) 2)
      "-"
      hex(uuid(5) 2)
      hex(uuid(6) 2)
      "-"
      hex(uuid(7) 2)
      hex(uuid(8) 2)
      "-"
      hex(uuid(9) 2)
      hex(uuid(10) 2)
      "-"
      hex(uuid(11) 2)
      hex(uuid(12) 2)
      hex(uuid(13) 2)
      hex(uuid(14) 2)
      hex(uuid(15) 2)
      hex(uuid(16) 2)
