#
  Copyright (C) 2024 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU Library General Public License, version 2, or
  (at your option) under the terms of the GNU Lesser General Public License,
  version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU Lesser (Library) General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files LGPLv2.txt and LGLPv3.txt or
  http://www.gnu.org/licenses/lgpl-2.0.html
  http://www.gnu.org/licenses/lgpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

$NONE .

$std::count:
  #
    counts items in a collection

    Returns the number of items in the specified collection that fullfill the
    specified criterion.

    If no *test*-function is given the collection must contain boolean values.

    If an *argument* is given the *test*-function is called with the collection
    item as the first argument and the *argument* as the second argument.

    Topic: Collections

    See also: any_of, all_of

    Example:

      $persons
	hash_table
	  "Gregory" = 39
	  "Julia" = 37
	  "Maria" = 33
	  "James" = 41
	  "Sarah" = 38

      println! count(persons less 40)

    Output:

      4
  (
    collection
    test = NONE
    argument = NONE
  )
  if
    NONE == test:
      map_reduce collection
	: (item)
	  if
	    item
	    -> 1
	    -> 0
	plus
	0
    :
      if
	NONE == argument:
	  map_reduce collection
	    : (item)
	      if
		test(item)
		-> 1
		-> 0
	    plus
	    0
	:
	  map_reduce collection
	    : (item)
	      if
		test(item argument)
		-> 1
		-> 0
	    plus
	    0

$std::any_of:
  #
    tests all items of a collection

    Returns <true> if at least one item in the specified collection fullfills
    the specified criterion.

    Topic: Collections

    See also: all_of, count

    Example:

      $persons
	hash_table
	  "Gregory" = 39
	  "Julia" = 37
	  "Maria" = 33
	  "James" = 41
	  "Sarah" = 38

      if
	any_of(persons less 40):
	  println! "true"
	:
	  println! "false"

    Output:

      true
  (
    collection # the collection which items to check
    test # the test function
    argument = NONE # additional argument that is passed to the test function
  )
  if
    argument == NONE:
      for_each collection
	: (item)
	  if
	    test(item)
	    -> true
	    next
	-> false
    :
      for_each collection
	: (item)
	  if
	    test(item argument)
	    -> true
	    next
	-> false

$std::all_of:
  #
    tests all items of a collection

    Returns <true> if all items in the specified collection fullfill the
    specified criterion.

    Topic: Collections

    See also: any_of, count

    Example:

      $persons
	hash_table
	  "Gregory" = 39
	  "Julia" = 37
	  "Maria" = 33
	  "James" = 41
	  "Sarah" = 38

      if
	all_of(persons less 40):
	  println! "true"
	:
	  println! "false"

    Output:

      false
  (
    collection # the collection which items to check
    test # the test function
    argument = NONE # additional argument that is passed to the test function
  )
  if
    argument == NONE:
      for_each collection
	: (item)
	  if
	    test(item)
	    next
	    -> false
	-> true
    :
      for_each collection
	: (item)
	  if
	    test(item argument)
	    next
	    -> false
	-> true

$std::apply:
  (
    function
    arguments*
  )
  if
    arguments.is_empty:
      function!
    :
      !arguments append(range(arguments 1 -2) arguments(-1))
      case length_of(arguments)
	1: function! arguments(1)
	2: function! arguments(1) arguments(2)
	3: function! arguments(1) arguments(2) arguments(3)
	4: function! arguments(1) arguments(2) arguments(3) arguments(4)
	5:
	  function!
	    arguments(1) arguments(2) arguments(3) arguments(4) arguments(5)
	6:
	  function!
	    arguments(1) arguments(2) arguments(3) arguments(4) arguments(5)
	    arguments(6)
	7:
	  function!
	    arguments(1) arguments(2) arguments(3) arguments(4) arguments(5)
	    arguments(6) arguments(7)
	8:
	  function!
	    arguments(1) arguments(2) arguments(3) arguments(4) arguments(5)
	    arguments(6) arguments(7) arguments(8)
	:
	  error "TOO MANY ARGUMENTS FOR APPLY"
