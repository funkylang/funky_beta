#
  Copyright (C) 2024 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU Library General Public License, version 2, or
  (at your option) under the terms of the GNU Lesser General Public License,
  version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU Lesser (Library) General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files LGPLv2.txt and LGLPv3.txt or
  http://www.gnu.org/licenses/lgpl-2.0.html
  http://www.gnu.org/licenses/lgpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,

$std::is_a_character ()
  #
    tests if an object is a character

    Topic: Strings

$std_types::object.is_a_character false
  #
    by default, an object is not a character

    Topic: Strings

$std_types::character.is_a_character true
  #
    a character is a character

    Topic: Strings

$std_types::character/match:
  #
    matches the start of a string with the character

    Result:
      len # 1 if the character is the first character of the string

    If the match is unsuccessful, then *len* is <undefined>.

    Topic: Strings

    See also: match_back, search

    Example:

      $str "abcdef"
      $len match('a' str)
      dump! `len

    Output:

      len: 1

    Example:

      $str "abcdef"
      $len match('z' str)
      dump! `len

    Output:

      len: <undefined>
  (
    self # the character to match with
    str # the string which's start is matched
  )
  if
    str.is_empty
    -> undefined
    :
      if
	str(1) == self
	-> 1
	-> undefined

$std_types::character/match_back:
  #
    matches the end of a string with the character

    Result:
      len # 1 if the character is the last character of the string

    If the match is unsuccessful, then *len* is <undefined>.

    Topic: Strings

    See also: match, search

    Example:

      $str "abcdef"
      $len match_back('f' str)
      dump! `len

    Output:

      len: 1

    Example:

      $str "abcdef"
      $len match_back('z' str)
      dump! `len

    Output:

      len: <undefined>
  (
    self # the character to match with
    str # the string which's end is matched
  )
  if
    str.is_empty
    -> undefined
    :
      if
	str(-1) == self
	-> 1
	-> undefined

$std_types::character/search:
  #
    searches for the character from the start or the end of a string

    Results:
      pos # the position of the first occurrence of the character
      len # 1 if successful

    If the character is not found, then *pos* and *len* are <undefined>.

    Topic: Strings

    See also: match, match_back

    Example:

      $str "abcdef"
      search $pos $len 'd' str
      dump! `pos `len

    Output:

      pos: 4
      len: 1

    Example:

      $str "abcdef"
      search $pos $len 'z' str
      dump! `pos `len

    Output:

      pos: <undefined>
      len: <undefined>

    Example:

      $str "In the wild, wild west we were alone."
      search $pos $len 'w' str 2
      dump! `pos `len

    Output:

      pos: 14
      len: 1

    Example:

      $str "In the wild, wild west we were alone."
      search $pos $len 'w' str -2
      dump! `pos `len

    Output:

      pos: 24
      len: 1
  (
    self # the character to search for
    stream # the string to search in
    nth = 1 # search for the nth occurrence; negative value search backwards
  )
  $i undefined
  $n length_of(stream)
  if
    nth < 0:
      !i n
      search_backwards
    :
      !i 1
      search_forwards

  $search_forwards:
    if
      i <= n
      :
	if
	  stream(i) == self:
	    if
	      nth > 1:
		dec &nth
		inc &i
		search_forwards
	      -> i 1
	  :
	    inc &i
	    search_forwards
      -> undefined undefined

  $search_backwards:
    if
      i >= 1
      :
	if
	  stream(i) == self:
	    if
	      nth < -1:
		inc &nth
		dec &i
		search_backwards
	      -> i 1
	  :
	    dec &i
	    search_backwards
      -> undefined undefined

$std::is_a_digit:
  #
    checks wheter its argument is a digit

    Result:
      success # true if the argument is a digit

    This function must be called on character-values only.

    Topic: Strings

    See also: std_types::character, is_a_letter, is_a_letter_character

    Example:

      if
	is_a_digit('3'):
	  println! "3 is a digit"
	:
	  println! "3 is not a digit"

    Output:

      3 is a digit

    Example:

      if
	is_a_digit('a'):
	  println! "a is a digit"
	:
	  println! "a is not a digit"

    Output:

      a is not a digit
  (
    chr # the character to check
  )
  -> chr >= '0' && chr <= '9'

$std::is_a_letter:
  #
    checks wheter its argument is a letter (upper or lower case)

    Result:
      success # true if the argument is a letter

    This function must be called on character-values only.

    Topic: Strings

    See also: is_an_uppercase_letter, is_a_lowercase_letter,
	      is_a_letter_character, is_a_digit

    Example:

      if
	is_a_letter('t'):
	  println! "t is a letter"
	:
	  println! "t is not a letter"

    Output:

      t is a letter

    Example:

      if
	is_a_letter('3'):
	  println! "3 is a letter"
	:
	  println! "3 is not a letter"

    Output:

     3 is not a letter
  (
    chr # the character to check
  )
  ->
    ||
      chr >= 'a' && chr <= 'z'
      chr >= 'A' && chr <= 'Z'

$std::is_an_uppercase_letter:
  #
    checks wheter its argument is an uppercase letter

    Result:
      success # true if the argument is an uppercase letter

    This function must be called on character-values only.

    Topic: Strings

    See also: is_a_letter, is_a_lowercase_letter,
	      is_a_letter_character, is_a_digit

    Example:

      if
	is_an_uppercase_letter('T'):
	  println! "T is an uppercase letter"
	:
	  println! "T is not an uppercase letter"

    Output:

      T is an uppercase letter

    Example:

      if
	is_an_uppercase_letter('t'):
	  println! "t is an uppercase letter"
	:
	  println! "t is not an uppercase letter"

    Output:

      t is not an uppercase letter
  (
    chr # the character to check
  )
  -> chr >= 'A' && chr <= 'Z'

$std::is_a_lowercase_letter:
  #
    checks wheter its argument is a lowercase letter

    Result:
      success # true if the argument is a lowercase letter

    This function must be called on character-values only.

    Topic: Strings

    See also: is_a_letter, is_an_uppercase_letter,
	      is_a_letter_character, is_a_digit

    Example:

      if
	is_a_lowercase_letter('t'):
	  println! "t is a lowercase letter"
	:
	  println! "t is not a lowercase letter"

    Output:

      t is a lowercase letter

    Example:

      if
	is_a_lowercase_letter('T'):
	  println! "T is a lowercase letter"
	:
	  println! "T is not a lowercase letter"

    Output:

      T is not a lowercase letter
  (
    chr # the character to check
  )
  -> chr >= 'a' && chr <= 'z'

$std_types::character/serialize:
  #
    serializes the character

    Result:
      str # the serialization of the character

    Topic: Strings

    The optional indent is obeyed.

    Example:

      $str serialize('a')
      println! str

    Output:

      'a'

    Example:

      $str serialize('a' 2)
      println! str

    Output:

	'a'
  (
    self # the character to serialize
    indent = undefined # the indentation depth in character positions
  )
  $str
    if
      self < '@0x20;' || self > '@0x7e;' || self == '@@'
      -> "'@@@(self.to_integer);'@nl;"
      -> "'@(self)'@nl;"
  if
    indent.is_defined
    -> append(tabs_and_spaces(indent) str)
    -> str

$std::to_wide ()

$std_types::character/to_wide: (chr)
  case chr
    '@0x21;' .. '@0x7e;' -> chr+0xfee0
    ' ' -> '@0x3000;'
    -> chr
