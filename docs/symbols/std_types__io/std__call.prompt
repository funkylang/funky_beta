[MANUAL]

In Funky function calls are either written on a single line with arguments
enclosed within parentheses and separated by single spaces, e.g.:

print! f(a b c) g(x y z)

or they are written in indentation style using mutliple lines:

print!
  f
    a
    b
    c
  g
    x
    y
    z

Functions that create any output or read any input need I/O-access rights.

A function that needs I/O-access rights must be called with a trailing
exclamation mark after the function name, e.g.:

println! "Hello, World!"

The expression

key = value

denotes a key-value-pair.

Collection types cannot be printed directly. You can use "dump!" to
dump the contents of a collection, e.g.:

$ages
  hash_table
    "John" = 42
    "Mary" = 25
    "Irene" = 49

dump! `ages

Output:

ages: hash_table <6>
  "Mary"
  25
  "Irene"
  49
  "John"
  42

Note: The number in angular brackets after the typename denotes the number
of (indented) lines that follow the head.

A tail call returns the result of the called function; e.g.:

$calc:
  (
    x
    y
  )
  plus x*x y*y

println! calc(2 3)

prints

13

[GUIDELINES]

All documentation remarks must have

* a single short (single line) description written in lower case

* a parameter(s) section (if appropriate)

* a result(s) section (if appropriate)

* a detailed description of how to use the symbol: when describing unique
  items of constants, describe how to use them;
  do not repeat yourself

* one or more associated topic(s) (if appropriate)

* a "See also" entry - mention only a view relevant symbols here

Do not mention unknown symbols!

Do not mention more than 10 symbols in the "See also" entry.

DO NOT MENTION MORE THAN 10 SYMBOLS IN THE "See also" ENTRY!

Do not mention internal details of functions.

Do not start each sentence in the description with "If" or "The".

DO NOT START EACH SENTENCE IN THE DESCRIPTION WITH "If" OR "The"!

Do not tell the user what a function does not do.

When referring to another symbol write it enclosed in angle brackets, e.g.
<std::clear>.

The "self"-parameter is a normal parameter like any other and should be
mentioned in the the parameter description.

Replace symbolic constants by their value.

Operations on I/O-objects are not executed immediately but queued as jobs.
To identify a job, an "id" is used.

I/O-functions and I/O-methods either need I/O-access rights or propagate
them ("if", "for_each", etc.).

An "example" section, if available, must use Funky syntax and shall not be too
long.

If there is an appropriate example in the source code then use this example
in the "example" section!

[EXAMPLES]

[source]

$std_types::screen/std::clear:
  (
    self
    x = 1
    y = 1
    width = undefined
    height = undefined
  )
  update_if width.is_undefined &width -> width_of(self)-x+1
  update_if height.is_undefined &height -> height_of(self)-y+1
  basic_private::clear self self x y width height

[documentation remark]

# std_types::screen/clear (a method)

  clears the specified rectangular area

  Parameters:
    self: the screen to be (partially) cleared
    x: x-coordinate of the upper left corner
    y: y-coordinate of the upper left corner
    width: width of the area
    height: height of the area

  Result:
    screen: the updated screen

  The specified area is filled with spaces using the the current draw-
  and clear colour.

  The area is clipped at the screen's borders.

  This function also works for "graphical screens" (line_height != 1).

  Topic: Terminal

  See also: std_types::screen/std::set_clear_colour,
            std_types::screen/std::copy

[source]

$std_types::string/match:
  (
    self
    stream
  )
  $rc result_count()
  $len length_of(self)
  if
    length_of(stream) >= len:
      if
        range(stream 1 len) == self:
          if
            rc == 1
            -> len
            -> len undefined
        fail
    fail

  $fail:
    if
      rc == 1
      -> undefined
      -> undefined undefined

[documentation remark]

# std_types::string/std::match (a method)

  matches the start of the stream with the string

  Parameters:
    self: the string to match
    stream: the stream to match against

  Results:
    length: the number of matched characters (or <undefined> on fail)
    capture: the matched string (if requested)

  Attempts to match the start of the stream with the specified string.

  If the match is successful the length of the match (= the length of the
  string) is returned.

  If the caller requested two result values then in addition to the string
  length the string itself ("the capture") is also returned.

  If the match fails then one or two <std::undefined>-values are returned.

  Topic: Strings

  See also: std_types::string/std::match_back, std_types::string/std::search

[source]

$std_types::generic_list/map_or_error:
  (
    self
    function
  )
  if
    self.is_empty
    -> self
    :
      $map
        if
          parameter_count_of(function) == 2
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  append_or_error
                    map(s m)
                    map(m+1 e)
                :
                  $mapped_item function(s self(s))
                  if
                    mapped_item.is_an_error
                    -> mapped_item
                    -> list(mapped_item)
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  append_or_error
                    map(s m)
                    map(m+1 e)
                :
                  $mapped_item function(self(s))
                  if
                    mapped_item.is_an_error
                    -> mapped_item
                    -> list(mapped_item)

      map 1 length_of(self)

[documentation remark]

# std_types::generic_list/std::map_or_error (a method)

  applies a function to all elements of the list

  Parameters:

    self: the list which items will be mapped
    function: the function to apply to each element of the list

  Result:
    list: the list of "mapped" items

  A list of the "mapped" items is returned.

  If any item ist mapped to an error object than the result ist not a
  collection but the first (left-to-right-order) error object created.

  Topic: Lists

  See also: std_types::generic_list/std::reduce,
            std_types::generic_list/std::map_reduce

[EXERCISE]

#
  Topic: Input-Output

  Input/Output is handled in a special way in Funky.

  I/O-functions must be called with so-called *I/O-access rights*.

  To call a function with I/O-access rights one

  * must already have *I/O-access rights*

  and

  * give them to the called function

  To give *I/O-access rights* to a function it is called with a "!"-suffix.

  Related topics: Input-Output Events, POSIX, Input-Output Task Manager

  Example:

    println! "Hello, world!"

  Output:

    Hello, world!

#
  Topic: POSIX

  The Funky-API includes most POSIX-function calls.

  The are wrapped in a Funky-style API.

  Funky uses mainly blocking I/O but makes heavy use of the <std::pselect>
  function to make I/O-calls only when non-blocking behaviour is guaranteed.

  In most cases it will be more appropriate to use the <@Input-Output>-API than
  the POSIX-API.

  Related topics: Input-Output

[symbols]


# File Operations

$std::load () # loads a file in some way; returns a string
$std::save () # saves a file in some way
$std::update ()

# Directory Entry Attributes

$std::is_a_directory ()
$std::sleep:
$std::write_string_to:
$std::write_to:
$std::writeln_to:
$std::print_string_to:
$std::print_to ()
$std_types::file_descriptor/print_to:
$std::println_to:
$std::print_string:
$std::print:
$std::println:
$std::eprint_string:
$std::eprint:
$std::eprintln:
$std::read_from:
$std::read_from_until:
$std::readln_from:
$std::read_all_from:
$std::get_from_until:
$std::getln_from:
$std::get_until:
$std::getln:
$std_types::string/load:
$std_types::string/save:
$std_types::file_descriptor/load:
$std_types::io.load_buffer_of empty_hash_table
$std_types::io.max_length_of empty_hash_table
$std_types::io.id_of empty_hash_table
$std_types::io.info_of empty_hash_table
$std_types::io/load:
$std_types::io/save:
$std::try:
$std::check:
$std::file_exists:
$std::directory:
$std_types::stat/is_a_directory: (self)
$std::call ()
$std_types::object/call:
$std_types::io/call:
$std::terminate:
$std::is_a_file_descriptor ()
$std_types::object.is_a_file_descriptor false
$std_types::file_descriptor.is_a_file_descriptor true
$std_types::file_descriptor.serialization_tag_of "file_descriptor"
$std_types::file_descriptor/serialize:
$std_types::file_descriptor.deserializer_of:
$std::is_a_process_id ()
$std_types::object/is_a_process_id false
$std_types::process_id/is_a_process_id true
$std_types::process_id.serialization_tag_of "process_id"
$std_types::process_id/serialize:
$std_types::process_id.deserializer_of:
$std::pipe:
$std::pipe_commands:
$std::which:
$std::find_first_process: (name)
$std::find_processes: (name)
$std::get_process_name: (pid)
$std::get_mac_address:
$std::extern:

$std::get_from_until:
  (
    fd
    sentinel
    nth = 1
  )
  read_from_until! $data fd sentinel nth
  -> data.from_utf8

$std::getln_from:
  (
    fd
  )
  get_from_until! fd '@nl;'

$std::get_until:
  (
    sentinel
  )
  get_from_until! STDIN_FILENO sentinel

$std::getln:
  getln_from! STDIN_FILENO

$std_types::string/load:
  #
    Example:

      load! $buf "test.dat"

    Example:

      load! $buf "/dev/sda" 512 # load first sector of /dev/sda
  (
    filename
    max_length = undefined
  )
  open! $fd filename "r"
  check fd
  if
    max_length.is_defined:
      read_from! $data fd max_length
      close! fd
      -> data
    :
      load! fd

$std_types::string/save:
  (
    filename
    data
  )
  open! $fh filename "w+"
  check fh
  write_to! fh data
  close! fh

$std_types::file_descriptor/load:
  (
    fd
  )
  read_all_from! $data fd
  check data
  close! fd
  -> data

$load_buffer_of ()

$max_length_of ()

$id_of ()

$info_of ()

$std_types::io.load_buffer_of empty_hash_table

$std_types::io.max_length_of empty_hash_table

$std_types::io.id_of empty_hash_table

$std_types::io.info_of empty_hash_table

$std_types::io/load:
  (
    io
    id
    filename
    max_length = undefined
    data = filename # the default data to return
  )
  run io load_file id filename max_length data

$load_file: (io id filename max_length data)
  open! $fd filename "r"
  if
    fd.is_an_error
    -> io tuple(JOB_FAILED id fd data)
    :
      start_reading_from &io fd true max_length
      !io.load_buffer_of(fd) ""
      !io.max_length_of(fd) max_length
      !io.id_of(fd) id
      register_handlers &io fd
        READ = read_data
        READ_FAILED = tuple(read_failed data)
        CLOSED = tuple(file_closed_after_reading data)
      -> io undefined

$read_data: (io fd data)
  $buf load_buffer_of(io)(fd)
  $max_length max_length_of(io)(fd)
  append &buf data
  if
    max_length.is_defined && length_of(buf) >= max_length:
      deregister_all_handlers &io fd
      $id id_of(io)(fd)
      !io.load_buffer_of(fd) undefined
      !io.max_length_of(fd) undefined
      !io.id_of(fd) undefined
      -> io tuple(JOB_COMPLETED id buf)
    :
      !io.load_buffer_of(fd) buf
      -> io undefined

$read_failed: (io fd err data)
  deregister_all_handlers &io fd
  !io.load_buffer_of(fd) undefined
  !io.max_length_of(fd) undefined
  !io.id_of(fd) undefined
  -> io tuple(JOB_FAILED fd err data)

$file_closed_after_reading: (io fd _dummy data)
  deregister_all_handlers &io fd
  $buf load_buffer_of(io)(fd)
  $id id_of(io)(fd)
  !io.load_buffer_of(fd) undefined
  !io.max_length_of(fd) undefined
  !io.id_of(fd) undefined
  -> io tuple(JOB_COMPLETED id buf data)

$NONE .

$std_types::io/save:
  (
    io
    id
    filename
    data
    context = NONE
  )
  run io save_file id filename data context

$save_file: (io id filename data context)
  open! $fd filename "w"
  if
    fd.is_an_error
    -> io tuple(JOB_FAILED id fd filename)
    :
      write &io fd data
      !io.id_of(fd) id
      update_if NONE == context &context -> filename
      register_handlers &io fd
        WRITE_COMPLETED = tuple(write_completed context)
        WRITE_FAILED = tuple(write_failed context)
      -> io undefined

$write_completed: (io fd _dummy context)
  run &io close_file_after_writing fd context
  -> io undefined

$close_file_after_writing: (io fd context)
  close! fd
  deregister_all_handlers &io fd
  $id id_of(io)(fd)
  !io.id_of(fd) undefined
  -> io tuple(JOB_COMPLETED id undefined context)

$write_failed: (io fd err context)
  deregister_all_handlers &io fd
  $id id_of(io)(fd)
  !io.id_of(fd) undefined
  -> io tuple(JOB_FAILED id err context)

$std::try:
  #
    Example:

      $buf undefined
      try $success: load! !buf "test.dat"
      if
        buf.is_an_error:
          println! "Failed to load test.dat"
        :
          # successfully loaded "test.dat"
          ...
  (
    body
  )
  body!
  -> undefined

$std::check:
  (
    argument
  )
  if
    argument.is_an_error
    -> argument
    pass

$std::file_exists:
  #
    Example:

      file_exists! $does_exist "test.dat"
      if
        does_exist:
          println! "test.dat exists"
        :
          println! "test.dat does not exist"
  (
    filename
  )
  stat! $info filename
  -> not(info.is_an_error)

$std::directory:
  #
    Example:

      directory! $entries "/usr/bin"
      for_each entries
        : (entry)
          println! name_of(entry)
          next!
        pass
  (
    options*
    pathname
  )
  get_options options
    SORT = false $do_sort
  $entries empty_list
  opendir! $dir pathname
  loop:
    readdir! $entry dir
    if
      entry.is_an_error:
        closedir! dir
        if
          do_sort:
            sort entries: (left right) -> name_of(left) < name_of(right)
          -> entries
      :
        push &entries entry
        next!

$std_types::stat/is_a_directory: (self)
  -> mode_of(self)(1) == 'd'

$std::call ()

$std_types::object/call:
  (
    cmd
    args
  )
  $rc result_count()
  $do_return_error_code (rc >= 1)
  $do_collect_output (rc >= 2)
  $do_collect_error_output (rc == 3)
  map &args to_string
  $env_vars empty_list
  for_each environment !env_vars
    : (key value)
      push &env_vars "@(key)=@(value)"
      next
    -> env_vars
  create_process! $pid $in_fd $out_fd $err_fd cmd args env_vars
  close! in_fd
  $io std_types::io
  join_process &io pid
  start_reading_from &io out_fd
  start_reading_from &io err_fd

  $out ""
  $err ""
  loop:
    get_events! &io $events
    for_each events
      : (event)
        event $type $fd $data
        case type
          READ:
            if
              data == ""
              next
              :
                case fd
                  out_fd:
                    if
                      do_collect_output:
                        append &out data
                        next!
                      :
                        write_to! STDOUT_FILENO data
                        next!
                  err_fd:
                    if
                      do_collect_error_output:
                        append &err data
                        next!
                      :
                        write_to! STDERR_FILENO data
                        next!
                  :
                    next!
          PROCESS_JOINED:
            $ret data
            if
              do_return_error_code:
                if
                  do_collect_output:
                    if
                      do_collect_error_output
                      -> ret out err
                      -> ret out
                  -> ret
              :
                if
                  ret == 0
                  pass
                  :
                    error "CALLING PROCESS FAILED"
          : # ignore all other signals
            next!
      next

$std_types::io/call:
  (
    io
    id
    cmd
    args
    context = NONE
  )
  run io start_process id cmd args context

$start_process: (io id cmd args context)
  map &args to_string
  $env_vars empty_list
  for_each environment !env_vars
    : (key value)
      push &env_vars "@(key)=@(value)"
      next
    -> env_vars
  create_process! $pid $in_fd $out_fd $err_fd cmd args env_vars
  close! in_fd
  join_process &io pid
  start_reading_from &io out_fd
  start_reading_from &io err_fd
  !io.id_of(pid) id
  !io.info_of(out_fd) ""
  !io.info_of(err_fd) ""
  !io.info_of(pid) tuple(out_fd err_fd)
  register_handlers &io out_fd
    READ = read_process_output
    READ_FAILED = read_from_process_failed
    CLOSED = ignore_close
  register_handlers &io err_fd
    READ = read_process_output
    READ_FAILED = read_from_process_failed
    CLOSED = ignore_close
  register_handlers &io pid
    PROCESS_JOINED = tuple(process_finished context)
  -> io undefined

$read_process_output: (io fd data)
  append &io.info_of(fd) data
  -> io undefined

$read_from_process_failed: (io fd _err)
  deregister_all_handlers &io fd
  -> io undefined

$ignore_close: (io _fd _data)
  -> io undefined

$process_finished: (io pid status context)
  log &io 4 "process finished with status @(status)"
  deregister_all_handlers &io pid
  $id id_of(io)(pid)
  !io.id_of(pid) undefined
  info_of(io)(pid) $out_fd $err_fd
  $out info_of(io)(out_fd)
  $err info_of(io)(err_fd)
  !io.info_of(pid) undefined
  !io.info_of(out_fd) undefined
  !io.info_of(err_fd) undefined
  if
    status == 0
    -> io tuple(JOB_COMPLETED id tuple(out err) context)
    -> io tuple(JOB_FAILED id tuple(status out err) context)

$std::terminate:
  exit! EXIT_SUCCESS

$std::is_a_file_descriptor ()

$std_types::object.is_a_file_descriptor false

$std_types::file_descriptor.is_a_file_descriptor true

$std_types::file_descriptor.serialization_tag_of "file_descriptor"

$std_types::file_descriptor/serialize:
  (
    self
    indent = undefined
  )
  $str "
    file_descriptor @(self.to_integer)
  if
    indent.is_defined
    -> append(tabs_and_spaces(indent) str)
    -> str

$std_types::file_descriptor.deserializer_of:
  (
    _type
    argument
  )
  -> file_descriptor(integer(argument))

register_type &deserializers std_types::file_descriptor

$std::is_a_process_id ()

$std_types::object/is_a_process_id false

$std_types::process_id/is_a_process_id true

$std_types::process_id.serialization_tag_of "process_id"

$std_types::process_id/serialize:
  (
    self
    indent = undefined
  )
  $str "
    process_id @(self.to_integer)
  if
    indent.is_defined
    -> append(tabs_and_spaces(indent) str)
    -> str

$std_types::process_id.deserializer_of:
  (
    _type
    argument
  )
  -> process_id(integer(argument))

register_type &deserializers std_types::process_id

$command_of ()

$extern_call:
  (
    myself^
    arguments*
  )
  call! command_of(myself) map(flatten(arguments) to_string)

$extern_call.command_of undefined

$std::pipe:
  (
    args*
  )
  $i 1
  $n length_of(args)
  $in_fd undefined
  $feed undefined
  $commands empty_list
  $head undefined
  $arguments undefined
  $first_arg args(1)
  if
    first_arg.is_a_string:
      !feed first_arg
      !i 2
      build_commands!
    :
      if
        first_arg.is_a_file_descriptor:
          !in_fd first_arg
          !i 2
          build_commands!
        build_commands

  $build_commands:
    loop
      :
        if
          i > n break
          :
            $arg args(i)
            if
              arg.is_a_function:
                update_if head.is_defined &commands:
                  push commands tuple(head map(flatten(arguments) to_string))
                !head command_of(arg)
                !arguments empty_list
                inc &i
                next!
              :
                push &arguments arg
                inc &i
                next!
      :
        update_if head.is_defined &commands:
          push commands tuple(head map(flatten(arguments) to_string))
        run_pipe! in_fd feed commands

$std::pipe_commands:
  (
    feed_or_fd = undefined
    commands
  )
  if
    feed_or_fd.is_defined:
      if
        feed_or_fd.is_a_file_descriptor:
          run_pipe! feed_or_fd undefined commands
        :
          run_pipe! undefined feed_or_fd commands
    :
      run_pipe! undefined undefined commands

[source]

$std::call ()

$std_types::io/call:
  (
    io
    id
    cmd
    args
    context = NONE
  )
  run io start_process id cmd args context

[documentation remark]

### Always copy examples contained in the symbol's description remark
    into the documentation

### Use (parts of) the documentation already provided in the source

### Do not create completely new examples on your own

### Use only one of the following topics: Input-Output, POSIX

# std_types::io/std::call (a method)

