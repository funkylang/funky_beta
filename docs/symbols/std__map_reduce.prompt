[MANUAL]

In Funky function calls are either written on a single line with arguments
enclosed within parentheses and separated by single spaces, e.g.:

print! f(a b c) g(x y z)

or they are written in indentation style using mutliple lines:

print!
  f
    a
    b
    c
  g
    x
    y
    z

Functions that create any output or read any input need I/O-access rights.

A function that needs I/O-access rights must be called with a trailing
exclamation mark after the function name, e.g.:

println! "Hello, World!"

The expression

key = value

denotes a key-value-pair.

Collection types cannot be printed directly. You can use "dump!" to
dump the contents of a collection, e.g.:

$ages
  hash_table
    "John" = 42
    "Mary" = 25
    "Irene" = 49

dump! `ages

Output:

ages: hash_table <6>
  "Mary"
  25
  "Irene"
  49
  "John"
  42

Note: The number in angular brackets after the typename denotes the number
of (indented) lines that follow the head.

A tail call returns the result of the called function; e.g.:

$calc:
  (
    x
    y
  )
  plus x*x y*y

println! calc(2 3)

prints

13

[GUIDELINES]

All documentation remarks must have

* a single short (single line) description written in lower case

* a parameter(s) section (if appropriate)

* a result(s) section (if appropriate)

* a detailed description of how to use the symbol: when describing unique
  items of constants, describe how to use them;
  do not repeat yourself

* one or more associated topic(s) (if appropriate)

* a "See also" entry - mention only a view relevant symbols here

Do not mention unknown symbols!

Do not mention more than 10 symbols in the "See also" entry.

DO NOT MENTION MORE THAN 10 SYMBOLS IN THE "See also" ENTRY!

Do not mention internal details of functions.

Do not start each sentence in the description with "If" or "The".

DO NOT START EACH SENTENCE IN THE DESCRIPTION WITH "If" OR "The"!

Do not tell the user what a function does not do.

When referring to another symbol write it enclosed in angle brackets, e.g.
<std::clear>.

The "self"-parameter is a normal parameter like any other and should be
mentioned in the the parameter description.

Replace symbolic constants by their value.

Operations on I/O-objects are not executed immediately but queued as jobs.
To identify a job, an "id" is used.

I/O-functions and I/O-methods either need I/O-access rights or propagate
them ("if", "for_each", etc.).

An "example" section, if available, must use Funky syntax and shall not be too
long.

If there is an appropriate example in the source code then use this example
in the "example" section!

[EXAMPLES]

[source]

$std::clear_colour_of ()

[documentation remark]

# std::clear_colour_of (a polymorphic function)

  the clear colour to use

  Parameter:
    self: the object from which to get the clear colour

  Result:
    colour: the clear colour

  Returns the clear colour of the object.

  Topic: Terminal

  See also: std::draw_colour_of, std::set_clear_colour

[source]

$std_types::screen/clear:
  (
    self
    x = 1
    y = 1
    width = undefined
    height = undefined
  )
  update_if width.is_undefined &width -> width_of(self)-x+1
  update_if height.is_undefined &height -> height_of(self)-y+1
  std_private::clear self self x y width height

$std::clear ()

[documentation remark]

# std::clear (a polymorphic function)

  clears a rectangular area

  Parameters:
    screen # some kind of screen
    x: x-coordinate of the upper left corner
    y: y-coordinate of the upper left corner
    width: width of the area
    height: height of the area

  Result:
    screen: the updated screen

  The area is cleared using the current clear colour. Use
  <std::set_clear_colour> to change the current clear colour.

  The area is clipped at the screen's borders.

  Topic: Terminal

  See also: std_types::screen/std::clear, std::draw_text,
            std::set_clear_colour

  Example:

    $my_screen create_screen(terminal 40 25)
    set_clear_colour &my_screen GREEN
    clear &my_screen 11 6 20 15

[EXERCISE]

#
  Topic: Collections

  There are several kinds of collections

  * tuples

  * lists

  * tables

[symbols]

$std_types::collection std_types::object
$std_types::collection/is_empty:
$std_types::collection/is_not_empty:
$std::first_key_of ()
$std::first_item_of ()
$std::last_key_of ()
$std::last_item_of ()
$std::next_key_of ()
$std::previous_key_of ()
$std::retrieve_first_item ()
$std::retrieve_last_item ()
$std::reverse ()
$std::for_each ()
$std::map ()
$std::append_or_error ()
$std::map_or_error ()
$std::reduce ()
$std::map_reduce ()
$std::find_first ()
$std::filter ()
$std::sort ()
$std::intersection ()
$std::merge ()
$std::zip ()
$std::flatten ()
$std::truncate ()
$std::pad_left ()
$std::pad_right ()
$std::has_prefix ()
$std::has_suffix ()
$std::without_prefix ()
$std::without_suffix ()
$std::before ()
$std::truncate_from ()
$std::behind ()
$std::truncate_until ()
$std::from ()
$std::truncate_before ()
$std::until ()
$std::truncate_behind ()
$std::between ()
$std::contains ()
$std::matches ()
$std::dup ()
$std::empty_collection_of ()
$std::to_base64 ()
$std::from_base64 ()
$std::sha1 ()

<namespace basic_collections>

#
  Topic: Collections

  There are several kinds of collections

  * tuples

  * lists

  * tables

$std_types::collection std_types::object
  #
    the prototype object for all kinds of collections

    Topic: Collections

$std_types::collection/is_empty:
  # default implementation
  (
    self
  )
  -> length_of(self) == 0

$std_types::collection/is_not_empty:
  # default implementation
  (
    self
  )
  -> not(self.is_empty)

$std::first_key_of ()
  #
    returns the first key of a non-empty collection

$std::first_item_of ()
  #
    returns the first item of a non-empty collection

$std::last_key_of ()
  #
    returns the last key of a non-empty collection

$std::last_item_of ()
  #
    returns the last item of a non-empty collection

$std::next_key_of ()
  #
    returns the key after the specified key

$std::previous_key_of ()
  #
    returns the key before the specified key

$std::retrieve_first_item ()
  #
    returns and removes the first item of a non-empty collection

$std::retrieve_last_item ()
  #
    returns and removes the last item of a non-empty collection

$std::reverse ()
  #
    reverse the order of all items of a collection

$std::for_each ()
  #
    iterates over all items of a collection

    Topic: Loops

    See also: std::next, std::break

$std::map ()
  #
    applies a function to all items of a collection

    The function results are collected in a collection of the same type as the
    original collection.

    The function applications might be executed in parallel!

    See also: reduce, map_reduce, filter

$std::append_or_error ()
  #
    appends two collections

    If the left collection is an error object then this error object is returned
    else if the right collection is an error object then this error object is
    returned otherwise the default <append> method is applied.

$std::map_or_error ()
  #
    applies a function to all items of a collection

    The function results are collected in a collection of the same type as the
    original collection.

    If any item ist mapped to an error object than the result ist not a
    collection but the first (left-to-right-order) error object created.

    The function applications might be executed in parallel!

    See also: reduce, map_reduce, filter

$std::reduce ()
  #
    computes a kind of sum from the items of a collection

    There is no guarantee in which order the values will be reduced (folded).

    So the the reduction function must obey to the law of assosiativity!

    See also: map, map_reduce, filter

$std::map_reduce ()
  #
    combines a map and a reduce operation into a single operation

    See also: map, reduce

$std::find_first ()
  #
    returns the first item of a collection that fullfills the condition

    See also: std::filter

$std::filter ()
  #
    returns a collection containing all the items that fullfill the
    filter-condition

    See also: std::map, std::find_first

$std::sort ()
  #
    returns a sorted copy of the collection

    If no comparison function is supplied then the items are sorted in ascending
    order using the <less>-method.

$std::intersection ()
  #
    computes the intersection of two collections

    The result shares type with the first collection.

$std::merge ()
  #
    merge two collections in a collection specific way

$std::zip ()
  #
    zip (merge) two collections

    All odd numbered items are taken from the first collection; all even
    numbered items from the second collection.

    If a collection runs out of items the remaining items of the other
    collection are simply appended.

$std::flatten ()
  #
    flattens a collection (probably a list)

$std::truncate ()
  #
    truncates a collection to the specified number of items

$std::pad_left ()

$std::pad_right ()

$std::has_prefix ()
  #
    returns *true* if the second argument is a prefix of the first argument

$std::has_suffix ()
  #
    returns *true* if the second argument is a suffix of the first argument

$std::without_prefix ()
  #
    removes a specific expression at the start of a string

    If the string specified as the first argument starts with the expression
    specified as the second argument, the matching prefix of the string is
    removed and the resulting string is returned.

    Otherwise the string is returned without alteration.

    Topic: Strings

    See also: std::without_suffix, std::has_prefix, std::has_suffix,
              std::behind, std::truncate_from

$std::without_suffix ()
  #
    removes a specific expression at the end of a string

    If the string specified as the first argument ends with the expression
    specified as the second argument, the matching suffix of the string is
    removed and the resulting string is returned.

    Otherwise the string is returned without alteration.

    Topic: Strings

    See also: std::without_prefix, std::has_suffix, std::has_prefix,
              std::before, std::truncate_until

$std::before ()
  #
    returns the substring before the specified expression

    If the string specified as the first argument contains the expression
    specified as the second argument, the substring before the first match of
    the expression is returned.

    **If the string does not contain the expression then the empty string is
    returned.**

    An optional third argument can be used to match the *nth* occurence of
    the specified expression. If this value is negative then -1 means the
    last occurrence, -2 means the next-to-last occurence and so on.

    Topic: Strings

    See also: std::truncate_from, std::between, std::behind, std::from, std::until

$std::truncate_from ()
  #
    returns the substring before the specified expression

    If the string specified as the first argument contains the expression
    specified as the second argument, the substring before the first match of
    the expression is returned.

    **If the string does not contain the expression then the string is
    returned without alteration.**

    An optional third argument can be used to match the *nth* occurence of
    the specified expression. If this value is negative then -1 means the
    last occurrence, -2 means the next-to-last occurence and so on.

    Topic: Strings

    See also: std::before, std::truncate_until,
              std::truncate_before, std::truncate_behind

$std::behind ()
  #
    returns the substring behind the specified expression

    If the string specified as the first argument contains the expression
    specified as the second argument, the substring behind the first match of
    the expression is returned.

    **If the string does not contain the expression then the empty string is
    returned.**

    An optional third argument can be used to match the *nth* occurence of
    the specified expression. If this value is negative then -1 means the
    last occurrence, -2 means the next-to-last occurence and so on.

    Topic: Strings

    See also: std::truncate_until, std::between, std::before, std::from, std::until

$std::truncate_until ()
  #
    returns the substring behind the specified expression

    If the string specified as the first argument contains the expression
    specified as the second argument, the substring behind the first match of
    the expression is returned.

    **If the string does not contain the expression then the string is
    returned without alteration.**

    An optional third argument can be used to match the *nth* occurence of
    the specified expression. If this value is negative then -1 means the
    last occurrence, -2 means the next-to-last occurence and so on.

    Topic: Strings

    See also: std::behind, std::truncate_from,
              std::truncate_before, std::truncate_behind

[source]

$std_types::generic_array/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate array to hold the "mapped" values.

    Topic: Arrays

    See also: map, reduce
  (
    self # the array to work on
    map_function # the function to apply to each element of the array
    reduce_function = append # the function to apply to pairs of values
    _unit = undefined # the value to return for empty arrays
  )
  $d dimension_count_of(self)
  case d
    1:
      $map_reduce
        if
          parameter_count_of(map_function) == 1
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  reduce_function
                    map_reduce(s m)
                    map_reduce(m+1 e)
                -> map_function(self(s))
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  reduce_function
                    map_reduce(s m)
                    map_reduce(m+1 e)
                -> map_function(s self(s))
      map_reduce 1 length_of(self)
    2:
      $n length_of(self 1)
      $m length_of(self 2)
      $acc neutral_element_of(map_function(self(1 1)))
      from_to 1 n
        : (i)
          if
            parameter_count_of(map_function) == 1:
              from_to 1 m
                : (j)
                  reduce_function &acc map_function(self(i j))
                  next
                next
            :
              from_to 1 m
                : (j)
                  reduce_function &acc map_function(i j self(i j))
                  next
                next
        -> acc
    3:
      $n length_of(self 1)
      $m length_of(self 2)
      $k length_of(self 3)
      $acc neutral_element_of(map_function(self(1 1 1)))
      from_to 1 n
        : (i)
          from_to 1 m
            : (j)
              if
                parameter_count_of(map_function) == 1:
                  from_to 1 k
                    : (k)
                      reduce_function &acc map_function(self(i j k))
                      next
                    next
                :
                  from_to 1 k
                    : (k)
                      reduce_function &acc map_function(i j k self(i j k))
                      next
                    next
            next
        -> acc
    :
      Error "
        <map_reduce> is not yet defined for arrays with more than three dimensions

$std_types::binary_table/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate table to hold the "mapped" values.

    Topic: Tables

    See also: map, reduce
  (
    self # the table to work on
    map_function # the function to apply to each element of the table
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty tables
  )
  $contents contents_of(self)
  if
    contents.is_an_empty_node
    -> unit
    :
      if
        parameter_count_of(map_function) == 2
        -> map_reduce_2(contents)
        -> map_reduce_1(contents)

  $map_reduce_1: (inner_node)
    inner_node $left $right $_my_key $my_value
    map_function &my_value
    if
      left.is_an_empty_node
      check_right
      :
        !my_value reduce_function(map_reduce_1(left) my_value)
        check_right

    $check_right:
      if
        right.is_an_empty_node
        -> my_value
        -> reduce_function(my_value map_reduce_1(right))

  $map_reduce_2: (inner_node)
    inner_node $left $right $my_key $my_value
    map_function my_key &my_value
    if
      left.is_an_empty_node
      check_right
      :
        !my_value reduce_function(map_reduce_2(left) my_value)
        check_right

    $check_right:
      if
        right.is_an_empty_node
        -> my_value
        -> reduce_function(my_value map_reduce_2(right))

$std_types::hash_table/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate table to hold the "mapped" values.

    Topic: Tables

    See also: map, reduce
  (
    self # the table to work on
    map_function # the function to apply to each element of the table
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty tables
  )
  if
    length_of(self) == 0
    -> unit
    :
      !mapping_function map_function
      !reduction_function reduce_function
      if
        parameter_count_of(map_function) == 2:
          map_reduce_2 contents_of(self)
        :
          map_reduce_1 contents_of(self)

$std_types::key_order_table/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate table to hold the "mapped" values.

    Topic: Tables

    See also: map, reduce
  (
    self # the table to work on
    map_function # the function to apply to each element of the table
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty tables
  )
  $contents contents_of(self)
  if
    contents.is_an_empty_node
    -> unit
    :
      if
        parameter_count_of(map_function) == 2
        -> map_reduce_2(contents)
        -> map_reduce_1(contents)

  $map_reduce_1: (node)
    node $_key $value $left $right
    map_function &value
    if
      left.is_an_empty_node
      check_right
      :
        !value reduce_function(map_reduce_1(left) value)
        check_right

    $check_right:
      if
        right.is_an_empty_node
        -> value
        -> reduce_function(value map_reduce_1(right))

  $map_reduce_2: (node)
    node $key $value $left $right
    map_function key &value
    if
      left.is_an_empty_node
      check_right
      :
        !value reduce_function(map_reduce_2(left) value)
        check_right

    $check_right:
      if
        right.is_an_empty_node
        -> value
        -> reduce_function(value map_reduce_2(right))

$std_types::insert_order_table/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate table to hold the "mapped" values.

    Topic: Tables

    See also: map, reduce
  (
    self # the table to work on
    map_function # the function to apply to each element of the table
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty tables
  )
  if
    self.is_empty
    -> unit
    :
      if
        parameter_count_of(map_function) == 2:
          $keys keys_of(self)
          $values values_of(self)
          map_reduce 1 length_of(self)

          $map_reduce: (s e)
            if
              s < e:
                $m (s+e) >> 1
                reduce_function
                  map_reduce(s m)
                  map_reduce(m+1 e)
              -> map_function(keys(s) values(s))
        :
          map_reduce values_of(self) map_function reduce_function unit

$std_types::hash_set/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate set to hold the "mapped" values.

    Topic: Tables

    See also: map, reduce
  (
    self # the set to work on
    map_function # the function to apply to each element of the set
    reduce_function = append # the function to apply to each member of the set
    unit = undefined # the value to return for empty sets
  )
  if
    length_of(self) == 0
    -> unit
    :
      !mapping_function map_function
      !reduction_function reduce_function
      map_reduce_1 contents_of(self)

$std_types::insert_order_set/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate table to hold the "mapped" values.

    Topic: Sets

    See also: map, reduce
  (
    self # the set to work on
    map_function # the function to apply to each element of the set
    reduce_function = append # the function to apply to each member of the set
    unit = undefined # the value to return for empty sets
  )
  map_reduce self.to_list map_function reduce_function unit

$std_types::key_order_set/map_reduce:
  #
    maps and reduces a set into a single value
  (
    self # the set to work on
    map_function # the function to apply to each element of the set
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty sets
  )
  if
    self.is_empty
    -> unit
    :
      $map_reduce: (node)
        node $key $left $right
        map_function &key
        if !key
          left.is_an_empty_node
          -> key
          -> reduce_function(map_reduce(left) key)
        if
          right.is_an_empty_node
          -> key
          -> reduce_function(key map_reduce(right))

      -> map_reduce(contents_of(self))

$std_types::generic_list/map_reduce:
  #
    combines a map and a reduce operation into a single operation

    Does not create an intermediate list to hold the "mapped" values.

    Topic: Lists

    See also: map, reduce
  (
    self # the list to work on
    map_function # the function to apply to each element of the list
    reduce_function = append # the function to apply to pairs of values
    unit = undefined # the value to return for empty lists
  )
  if
    result_count() == 2: # updates the list *and* returns the reduction
      if
        self.is_empty
        -> self unit
        :
          $map_reduce
            if
              parameter_count_of(map_function) == 2
              ->
                : (s e)
                  if
                    s < e:
                      $m (s+e) >> 1
                      map_reduce s m $left_items $left_value
                      map_reduce m+1 e $right_items $right_value
                      ->
                        append(left_items right_items)
                        reduce_function(left_value right_value)
                    :
                      map_function s self(s) $item $value
                      -> list(item) value
              ->
                : (s e)
                  if
                    s < e:
                      $m (s+e) >> 1
                      map_reduce s m $left_items $left_value
                      map_reduce m+1 e $right_items $right_value
                      ->
                        append(left_items right_items)
                        reduce_function(left_value right_value)
                    :
                      map_function self(s) $item $value
                      -> list(item) value
          map_reduce 1 length_of(self)
    :
      if
        self.is_empty
        -> unit
        :
          $map_reduce
            if
              parameter_count_of(map_function) == 2
              ->
                : (s e)
                  if
                    s < e:
                      $m (s+e) >> 1
                      reduce_function
                        map_reduce(s m)
                        map_reduce(m+1 e)
                    -> map_function(s self(s))
              ->
                : (s e)
                  if
                    s < e:
                      $m (s+e) >> 1
                      reduce_function
                        map_reduce(s m)
                        map_reduce(m+1 e)
                    -> map_function(self(s))

          map_reduce 1 length_of(self)

$std_types::value_range/map_reduce:
  (
    self
    map_function
    reduce_function = append
  )
  $map_reduce: (from to)
    $n to+1-from
    if
      n >= 2:
        $m n >> 1
        reduce_function
          map_reduce(from from+m-1)
          map_reduce(from+m to)
      -> map_function(from)

  self $lower_bound $upper_bound
  map_reduce lower_bound upper_bound

$std::map_reduce ()
  #
    combines a map and a reduce operation into a single operation

    See also: map, reduce

[documentation remark]

### Always copy examples contained in the symbol's description remark
    into the documentation

### Do not create completely new examples on your own

### Use only one of the following topics: Collections

# std::map_reduce (a polymorphic function)

