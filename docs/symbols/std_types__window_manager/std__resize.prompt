[MANUAL]

In Funky function calls are either written on a single line with arguments
enclosed within parentheses and separated by single spaces, e.g.:

print! f(a b c) g(x y z)

or they are written in indentation style using mutliple lines:

print!
  f
    a
    b
    c
  g
    x
    y
    z

Functions that create any output or read any input need I/O-access rights.

A function that needs I/O-access rights must be called with a trailing
exclamation mark after the function name, e.g.:

println! "Hello, World!"

The expression

key = value

denotes a key-value-pair.

Collection types cannot be printed directly. You can use "dump!" to
dump the contents of a collection, e.g.:

$ages
  hash_table
    "John" = 42
    "Mary" = 25
    "Irene" = 49

dump! `ages

Output:

ages: hash_table <6>
  "Mary"
  25
  "Irene"
  49
  "John"
  42

Note: The number in angular brackets after the typename denotes the number
of (indented) lines that follow the head.

A tail call returns the result of the called function; e.g.:

$calc:
  (
    x
    y
  )
  plus x*x y*y

println! calc(2 3)

prints

13

[GUIDELINES]

All documentation remarks must have

* a single short (single line) description written in lower case

* a parameter(s) section (if appropriate)

* a result(s) section (if appropriate)

* a detailed description of how to use the symbol: when describing unique
  items of constants, describe how to use them;
  do not repeat yourself

* one or more associated topic(s) (if appropriate)

* a "See also" entry - mention only a view relevant symbols here

Do not mention unknown symbols!

Do not mention more than 10 symbols in the "See also" entry.

DO NOT MENTION MORE THAN 10 SYMBOLS IN THE "See also" ENTRY!

Do not mention internal details of functions.

Do not start each sentence in the description with "If" or "The".

DO NOT START EACH SENTENCE IN THE DESCRIPTION WITH "If" OR "The"!

Do not tell the user what a function does not do.

When referring to another symbol write it enclosed in angle brackets, e.g.
<std::clear>.

The "self"-parameter is a normal parameter like any other and should be
mentioned in the the parameter description.

Replace symbolic constants by their value.

Operations on I/O-objects are not executed immediately but queued as jobs.
To identify a job, an "id" is used.

I/O-functions and I/O-methods either need I/O-access rights or propagate
them ("if", "for_each", etc.).

An "example" section, if available, must use Funky syntax and shall not be too
long.

If there is an appropriate example in the source code then use this example
in the "example" section!

[EXAMPLES]

[source]

$std_types::screen/std::clear:
  (
    self
    x = 1
    y = 1
    width = undefined
    height = undefined
  )
  update_if width.is_undefined &width -> width_of(self)-x+1
  update_if height.is_undefined &height -> height_of(self)-y+1
  std_private::clear self self x y width height

[documentation remark]

# std_types::screen/clear (a method)

  clears the specified rectangular area

  Parameters:
    self: the screen to be (partially) cleared
    x: x-coordinate of the upper left corner
    y: y-coordinate of the upper left corner
    width: width of the area
    height: height of the area

  Result:
    screen: the updated screen

  The specified area is filled with spaces using the the current draw-
  and clear colour.

  The area is clipped at the screen's borders.

  This function also works for "graphical screens" (line_height != 1).

  Topic: Terminal

  See also: std_types::screen/std::set_clear_colour,
            std_types::screen/std::copy

[source]

$std_types::string/match:
  (
    self
    stream
  )
  $rc result_count()
  $len length_of(self)
  if
    length_of(stream) >= len:
      if
        range(stream 1 len) == self:
          if
            rc == 1
            -> len
            -> len undefined
        fail
    fail

  $fail:
    if
      rc == 1
      -> undefined
      -> undefined undefined

[documentation remark]

# std_types::string/std::match (a method)

  matches the start of the stream with the string

  Parameters:
    self: the string to match
    stream: the stream to match against

  Results:
    length: the number of matched characters (or <undefined> on fail)
    capture: the matched string (if requested)

  Attempts to match the start of the stream with the specified string.

  If the match is successful the length of the match (= the length of the
  string) is returned.

  If the caller requested two result values then in addition to the string
  length the string itself ("the capture") is also returned.

  If the match fails then one or two <std::undefined>-values are returned.

  Topic: Strings

  See also: std_types::string/std::match_back, std_types::string/std::search

[source]

$std_types::generic_list/map_or_error:
  (
    self
    function
  )
  if
    self.is_empty
    -> self
    :
      $map
        if
          parameter_count_of(function) == 2
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  append_or_error
                    map(s m)
                    map(m+1 e)
                :
                  $mapped_item function(s self(s))
                  if
                    mapped_item.is_an_error
                    -> mapped_item
                    -> list(mapped_item)
          ->
            : (s e)
              if
                s < e:
                  $m (s+e) >> 1
                  append_or_error
                    map(s m)
                    map(m+1 e)
                :
                  $mapped_item function(self(s))
                  if
                    mapped_item.is_an_error
                    -> mapped_item
                    -> list(mapped_item)

      map 1 length_of(self)

[documentation remark]

# std_types::generic_list/std::map_or_error (a method)

  applies a function to all elements of the list

  Parameters:

    self: the list which items will be mapped
    function: the function to apply to each element of the list

  Result:
    list: the list of "mapped" items

  A list of the "mapped" items is returned.

  If any item ist mapped to an error object than the result ist not a
  collection but the first (left-to-right-order) error object created.

  Topic: Lists

  See also: std_types::generic_list/std::reduce,
            std_types::generic_list/std::map_reduce

[EXERCISE]

#
  Topic: Window Manager

[symbols]

$std::draw_vertical_slider ()
$std::draw_horizontal_slider ()

# Terminal Methods

$std::create_window_manager ()

# Window Manager Methods

$std::create_window ()
$std::shrink ()
$std::enlarge ()
$std::move_left ()
$std::move_right ()
$std::move_up ()
$std::move_down ()
$std::move_page_up ()
$std::move_page_down ()
$std::move_home ()
$std::move_end ()
$std::move_top ()
$std::move_bottom ()
$std::window_count_of ()
$std::set_title ()
$std::get_title ()
$std::set_buttons ()
$std::get_buttons ()
$std::set_content ()
$std::get_content ()
$std::get_window_at ()
$std::focus_next_window ()
$std::focus_previous_window ()
$std::focus_window ()
$std::set_selection ()
$std::unset_selection ()

# Create Window Options

$std::CONTENT .
$std::HORIZONTAL_SCROLLBAR .
$std::VERTICAL_SCROLLBAR .
$std::BACK_BUTTON .
$std::FORWARD_BUTTON .
$std::SHRINK_BUTTON .
$std::ENLARGE_BUTTON .
$std::CLOSE_BUTTON .
$std::FIRST_VISIBLE_COLUMN .
$std::FIRST_VISIBLE_ROW .
$std::STATE .
$std::DEFAULT .
$std::MINIMIZED .
$std::MAXIMIZED .
$std_types::window_manager std_types::object
$std_types::window_manager.windows_of empty_list
$std_types::window_manager.next_window_id_of 1
$std_types::window_manager.focused_of undefined
$std_types::window_manager.has_a_valid_layout false
$std_types::window_manager.has_a_new_size false
$std_types::terminal/create_window_manager:
$std_types::window_manager/create_window:
$std_types::window_manager/update:
$std_types::terminal/draw_vertical_slider:
$std_types::terminal/draw_horizontal_slider:
$std_types::window_manager/move_left:
$std_types::window_manager/move_right:
$std_types::window_manager/move_up:
$std_types::window_manager/move_page_up:
$std_types::window_manager/move_down:
$std_types::window_manager/move_page_down:
$std_types::window_manager/move_home:
$std_types::window_manager/move_end:
$std_types::window_manager/move_top:
$std_types::window_manager/move_bottom:
$std_types::window_manager/focus_next_window:
$std_types::window_manager/focus_previous_window:
$std_types::window_manager/focus_window:
$std_types::window_manager/close:
$std_types::window_manager/shrink:
$std_types::window_manager/enlarge:
$std_types::window_manager/window_count_of:
$std_types::window_manager/set_title:
$std_types::window_manager/get_title:
$std_types::window_manager/set_buttons:
$std_types::window_manager/get_buttons:
$std_types::window_manager/set_content:
$std_types::window_manager/get_content:
$std_types::window_manager/set_selection:
$std_types::window_manager/unset_selection:
$std_types::window_manager/resize:
$std_types::window_manager/get_window_at:
$std_types::window_manager/handle_events:

$std_types::window_manager/focus_window:
  (
    self
    id
  )
  $windows windows_of(self)
  for_each windows
    : (window)
      if
        id_of(window) == id
        -> self(.focused_of id)
        next
    -> self # window not found

$std_types::window_manager/close:
  (
    self
    id = undefined
  )
  $focused focused_of(self)
  update_if id.is_undefined &id -> focused
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      !windows append(range(windows 1 index-1) range(windows index+1 -1))
      !self.has_a_valid_layout false
      if
        windows.is_empty
        ->
          self
            .windows_of windows
            .focused_of undefined
        :
          update_if index > length_of(windows) &index -> 1
          update_if all_of(windows: (window) -> state_of(window) == MINIMIZED)
            &windows(index).state_of -> DEFAULT
          update_if index == focused &self.focused_of -> id_of(windows(index))
          -> self(.windows_of windows)
    -> self

$std_types::window_manager/shrink:
  (
    self
    id = undefined
  )
  #
    if a window is set to MINIMIZED then at least one other window must be set
    to DEFAULT or MAXIMIZED

  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      case state_of(window)
        DEFAULT:
          !window
            .state_of MINIMIZED
            .desired_state_of MINIMIZED
          !windows(index) window
          !self.has_a_valid_layout false
          if
            all_of(windows: (window) -> state_of(window) == MINIMIZED):
              $next_index
                if
                  index < length_of(windows)
                  -> index+1
                  -> 1
              !windows(next_index).state_of DEFAULT
              -> self(.windows_of windows)
            -> self(.windows_of windows)
        MAXIMIZED:
          !window
            .state_of DEFAULT
            .desired_state_of DEFAULT
          !windows(index) window
          map &windows: (window) -> window(.state_of desired_state_of(window))
          ->
            self
              .windows_of windows
              .has_a_valid_layout false
    -> self

$std_types::window_manager/enlarge:
  (
    self
    id = undefined
  )
  #
    if a window is set to MAXIMIZED then all other windows are set to MINIMIZED

  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      case state_of(window)
        MINIMIZED:
          !window
            .state_of DEFAULT
            .desired_state_of DEFAULT
          !windows(index) window
          find_first $maximized_idx $_maximized_window
            windows: (window) -> state_of(window) == MAXIMIZED
          !self.has_a_valid_layout false
          if
            maximized_idx.is_defined:
              !windows(maximized_idx).state_of DEFAULT
              -> self(.windows_of windows)
            -> self(.windows_of windows)
        DEFAULT:
          map &self.windows_of: (idx window)
            if
              idx == index
              -> window(.state_of MAXIMIZED)
              -> window(.state_of MINIMIZED)
          -> self(.has_a_valid_layout false)
    -> self

$std_types::window_manager/window_count_of:
  (
    self
  )
  -> length_of(windows_of(self))

$std_types::window_manager/set_title:
  (
    self
    id = undefined
    title
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      !window.title_of title
      -> self(.windows_of windows(index window))
    -> self

$std_types::window_manager/get_title:
  (
    self
    id = undefined
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      -> title_of(window)
    -> undefined

$std_types::window_manager/set_buttons:
  (
    self
    id = undefined
    buttons
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      !window.additional_buttons_of buttons
      -> self(.windows_of windows(index window))
    -> self

$std_types::window_manager/get_buttons:
  (
    self
    id = undefined
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      -> additional_buttons_of(window)
    -> undefined

$std_types::window_manager/set_content:
  (
    self
    id = undefined
    content
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      !window.content_of content
      ->
        self
          .windows_of windows(index window)
          .has_a_valid_layout false
    -> self

$std_types::window_manager/get_content:
  (
    self
    id = undefined
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      -> content_of(windows(index))
    -> undefined

$std_types::window_manager/set_selection:
  (
    self
    id = undefined
    x
    y
    w
    h
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      $first_visible_column first_visible_column_of(window)
      $first_visible_row first_visible_row_of(window)
      $window_width width_of(window)
      $window_height height_of(window)
      $new_first_visible_column first_visible_column
      $new_first_visible_row first_visible_row
      update_if x < first_visible_column &new_first_visible_column -> x
      update_if x+w > first_visible_column+window_width
        &new_first_visible_column -> x+w-window_width
      update_if y < first_visible_row &new_first_visible_row -> y
      update_if y+h > first_visible_row+window_height
        &new_first_visible_row -> y+h-window_height
      !window
        .selection_of tuple(x y w h)
        .first_visible_column_of new_first_visible_column
        .first_visible_row_of new_first_visible_row
      -> self(.windows_of windows(index window))
    -> self

$std_types::window_manager/unset_selection:
  (
    self
    id = undefined
  )
  update_if id.is_undefined &id -> focused_of(self)
  $windows windows_of(self)
  $index find_index(windows id)
  if
    index.is_defined:
      $window windows(index)
      !window.selection_of undefined
      -> self(.windows_of windows(index window))
    -> self

$std_types::window_manager/resize:
  (
    self
    width
    height
  )
  self
    .width_of width
    .height_of height
    .has_a_valid_layout false
    .has_a_new_size true

$update_size: (wm terminal)
  if
    wm.has_a_new_size
    ->
      wm(.has_a_new_size false)
      resize(terminal width_of(wm) height_of(wm))
    ->
      wm
        .width_of width_of(terminal)
        .height_of height_of(terminal)
      terminal

$std_types::window_manager/get_window_at:
  (
    self
    x
    y
  )
  $windows windows_of(self)
  for_each windows
    : (window)
      $win_x x_of(window)
      $win_y y_of(window)
      $win_w width_of(window)
      $win_h height_of(window)
      if
        x >= win_x && x < win_x+win_w && y >= win_y && y < win_y+win_h
        ->
          id_of(window)
          content_of(window)
          first_visible_column_of(window)+x-win_x
          first_visible_row_of(window)+y-win_y
        next
    -> undefined undefined undefined undefined

$std_types::window_manager/handle_events:
  (
    wm
    events
    wm_fd
  )
  $new_events empty_list
  for_each events
    : (event)
      event $type $fd $argument
      case type
        MOUSE_PRESS:
          if
            fd == wm_fd:
              argument $x $y $button
              if
                button == 0:
                  handle_mouse_press &wm $new_event wm_fd x y
                  if
                    new_event.is_defined:
                      if
                        new_event.is_a_tuple:
                          push &new_events new_event
                          next
                        next
                    keep_event
                keep_event
            keep_event
        KEY_PRESS:
          case argument
            CURSOR_LEFT:
              move_left &wm
              next
            CURSOR_RIGHT:
              move_right &wm
              next
            CURSOR_UP:
              move_up &wm
              next
            CURSOR_DOWN:
              move_down &wm
              next
            PAGE_UP:
              move_page_up &wm
              next
            PAGE_DOWN:
              move_page_down &wm
              next
            HOME:
              move_home &wm
              next
            END:
              move_end &wm
              next
            CTRL_HOME:
              move_top &wm
              next
            CTRL_END:
              move_bottom &wm
              next
            CTRL_CURSOR_RIGHT:
              focus_next_window &wm
              next
            CTRL_CURSOR_LEFT:
              focus_previous_window &wm
              next
            CTRL_W:
              close &wm
              next
            keep_event
        NEW_WINDOW_SIZE:
          argument $width $height
          resize &wm width height
          next
        keep_event

      $keep_event:
        push &new_events event
        next
    -> wm new_events

$handle_mouse_press: (wm fd x y)
  $windows windows_of(wm)
  for_each windows
    : (idx window)
      $window_id id_of(window)
      $window_x x_of(window)
      $window_y y_of(window)
      $window_width width_of(window)
      $window_height height_of(window)
      if
        &&
          y == window_y-1
          x >= window_x && x < window_x+window_width
        :
          update_if window.needs_a_vertical_scrollbar
            &window_width -> window_width+2
          !wm.focused_of id_of(window)
          $buttons buttons_of(window)
          $bx window_x+window_width+1-3*length_of(buttons)
          for_each buttons
            : (button)
              if
                x >= bx && x < bx+2:
                  case button
                    SHRINK_BUTTON:
                      shrink &wm window_id
                      -> wm true
                    ENLARGE_BUTTON:
                      enlarge &wm window_id
                      -> wm true
                    CLOSE_BUTTON:
                      close &wm window_id
                      -> wm true
                    BACK_BUTTON -> wm tuple(KEY_PRESS fd BACK_BUTTON)
                    FORWARD_BUTTON -> wm tuple(KEY_PRESS fd FORWARD_BUTTON)
                    :
                      debug::dump `button
                      break
                :
                  plus &bx 3
                  next
            -> wm true
        :
          cond
            ->
              &&
                window.needs_a_vertical_scrollbar
                x >= window_x+window_width && x < window_x+window_width+2
                y >= window_y && y < window_y+window_height
              :
                !wm.focused_of id_of(window)
                minus &y window_y
                $content content_of(window)
                $content_height height_of(content)
                !window.first_visible_row_of
                  +
                    1
                    div
                      y*(content_height-window_height)+(window_height-1 >> 1)
                      window_height-1
                !wm.windows_of windows(idx window)
                -> wm true
            ->
              &&
                window.needs_a_horizontal_scrollbar
                x >= window_x && x < window_x+window_width
                y == window_y+window_height
              :
                !wm.focused_of id_of(window)
                minus &x window_x
                $content content_of(window)
                $content_width width_of(content)
                !window.first_visible_column_of
                  +
                    1
                    div
                      x*(content_width-window_width)+(window_width-1 >> 1)
                      window_width-1
                !wm.windows_of windows(idx window)
                -> wm true
            -> true next
    -> wm undefined

[source]

$std::resize ()

$std_types::window_manager/resize:
  (
    self
    width
    height
  )
  self
    .width_of width
    .height_of height
    .has_a_valid_layout false
    .has_a_new_size true

[documentation remark]

### Always copy examples contained in the symbol's description remark
    into the documentation

### Use (parts of) the documentation already provided in the source

### Do not create completely new examples on your own

### Use only one of the following topics: Window Manager

# std_types::window_manager/std::resize (a method)

