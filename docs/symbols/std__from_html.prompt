[MANUAL]

In Funky function calls are either written on a single line with arguments
enclosed within parentheses and separated by single spaces, e.g.:

print! f(a b c) g(x y z)

or they are written in indentation style using mutliple lines:

print!
  f
    a
    b
    c
  g
    x
    y
    z

Functions that create any output or read any input need I/O-access rights.

A function that needs I/O-access rights must be called with a trailing
exclamation mark after the function name, e.g.:

println! "Hello, World!"

The expression

key = value

denotes a key-value-pair.

Collection types cannot be printed directly. You can use "dump!" to
dump the contents of a collection, e.g.:

$ages
  hash_table
    "John" = 42
    "Mary" = 25
    "Irene" = 49

dump! `ages

Output:

ages: hash_table <6>
  "Mary"
  25
  "Irene"
  49
  "John"
  42

Note: The number in angular brackets after the typename denotes the number
of (indented) lines that follow the head.

A tail call returns the result of the called function; e.g.:

$calc:
  (
    x
    y
  )
  plus x*x y*y

println! calc(2 3)

prints

13

[GUIDELINES]

All documentation remarks must have

* a single short (single line) description written in lower case

* a parameter(s) section (if appropriate)

* a result(s) section (if appropriate)

* a detailed description of how to use the symbol: when describing unique
  items of constants, describe how to use them;
  do not repeat yourself

* one or more associated topic(s) (if appropriate)

* a "See also" entry - mention only a view relevant symbols here

Do not mention unknown symbols!

Do not mention more than 10 symbols in the "See also" entry.

DO NOT MENTION MORE THAN 10 SYMBOLS IN THE "See also" ENTRY!

Do not mention internal details of functions.

Do not start each sentence in the description with "If" or "The".

DO NOT START EACH SENTENCE IN THE DESCRIPTION WITH "If" OR "The"!

Do not tell the user what a function does not do.

When referring to another symbol write it enclosed in angle brackets, e.g.
<std::clear>.

The "self"-parameter is a normal parameter like any other and should be
mentioned in the the parameter description.

Replace symbolic constants by their value.

Operations on I/O-objects are not executed immediately but queued as jobs.
To identify a job, an "id" is used.

I/O-functions and I/O-methods either need I/O-access rights or propagate
them ("if", "for_each", etc.).

An "example" section, if available, must use Funky syntax and shall not be too
long.

If there is an appropriate example in the source code then use this example
in the "example" section!

[EXAMPLES]

[source]

$std::wrap_words:
  (
    text
    width
  )
  $lines empty_list
  $n length_of(text)
  $s 1 # start of line
  $b undefined # possible break position
  $i 0
  loop:
    inc &i
    if
      i > n:
        if
          s > n
          -> lines
          -> push(lines range(text s n))
      :
        $chr text(i)
        case chr
          '
':
            push &lines range(text s i-1)
            !s i+1
            !b undefined
            next
          :
            if
              chr == ' ':
                if
                  i-s == width: # break
                    push &lines range(text s i-1)
                    !s i+1
                    !b undefined
                    next
                  :
                    !b i
                    next
              :
                if
                  i-s < width
                  next
                  :
                    if
                      b.is_defined: # wrap
                        push &lines range(text s b-1)
                        !s b+1
                        !b undefined
                        next
                      : # hard break
                        push &lines range(text s i-1)
                        !s i
                        next

[documentation remark]

# std::wrap_words (a function)

  wraps a string consisting of words onto multiple lines

  Parameters:
    text: the text to wrap
    width: the maximum width of a line

  Result:
    lines: a list of (wrapped) lines

  The string is split into multiple lines according to the specified line
  width.

  Topic: Strings

  See also: std::indented

  Example:

    $str "This is a very long string that should be wrapped onto multiple lines"
    $lines wrap_words(str 30)
    dump! 2 `lines

  Output:

    lines:
      list
        "This is a very long string"
        "that should be wrapped"
        "onto multiple lines"

[EXERCISE]

[symbols]


# Topic: HTML

$std_types::html_element tuple(undefined undefined undefined)
$std::is_an_html_element ()
$std_types::html_element.is_an_html_element true
$std_types::object.is_an_html_element false
$std::from_html: (text)

#
  Copyright (C) 2024 by
  Dipl.-Ing. Michael Niederle

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU Library General Public License, version 2, or
  (at your option) under the terms of the GNU Lesser General Public License,
  version 3.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU Lesser (Library) General Public License for more details.

  For details of the GNU General Public License see the accompanying
  files LGPLv2.txt and LGLPv3.txt or
  http://www.gnu.org/licenses/lgpl-2.0.html
  http://www.gnu.org/licenses/lgpl-3.0.html
  or write to the
  Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.

# Topic: HTML

$std_types::html_element tuple(undefined undefined undefined)
  #
    tag (string)
    attributes (insert order table)
    contents (list)

$std::is_an_html_element ()

$std_types::html_element.is_an_html_element true

$std_types::object.is_an_html_element false

$is_a_void_element undefined

$std::from_html: (text)
  !is_a_void_element
    hash_set
      "area"
      "base"
      "br"
      "col"
      "embed"
      "hr"
      "img"
      "input"
      "keygen"
      "link"
      "meta"
      "param"
      "source"
      "track"
      "wbr"
  parse_html &text $element
  if
    element.is_defined && text .matches. some(WHITESPACE)
    -> element
    :
      error PARSE_ERROR "Unexpected input" text

$parse_html: (text)
  $i 1
  $n length_of(text)
  parse_element !i $top_level_element
  -> range(text i n) top_level_element

  $parse_element:
    loop:
      if
        i > n
        : error PARSE_ERROR "Unexpected end of input" ""
        :
          $chr text(i)
          if
            chr <= ' ':
              inc &i
              next
            :
              if
                chr == '<':
                  inc &i
                  parse_tag !i $tag
                  parse_attributes text !i $attributes
                  if
                    i > n || text(i) != '>'
                    :
                      error PARSE_ERROR "Expected '>'" range(text i -1)
                    :
                      inc &i
                      if
                        is_a_void_element(tag)
                        -> i new(std_types::html_element tag attributes undefined)
                        :
                          parse_contents !i $contents
                          if
                            i+1 > n || text(i) != '<' || text(i+1) != '/':
                              error PARSE_ERROR
                                "Expected closing tag"
                                range(text i -1)
                            :
                              plus &i 2
                              parse_tag !i $end_tag
                              if
                                end_tag != tag
                                :
                                  error PARSE_ERROR
                                    "
                                      Expected "</@(tag)>"
                                    range(text i -1)

                                :
                                  if
                                    text(i) != '>'
                                    :
                                      error PARSE_ERROR
                                        "Expected '>'"
                                        range(text i -1)
                                    -> i+1 new(std_types::html_element tag attributes contents)
                :
                  if
                    chr <= ' ':
                      inc &i
                      next
                    :
                      error PARSE_ERROR "Expected '<'" range(text i -1)

  $parse_tag:
    $s i
    loop:
      if
        i > n:
          error PARSE_ERROR "Expected '>'" range(text s -1)
        :
          $chr text(i)
          if
            chr <= ' ' || chr == '>'
            -> i range(text s i-1)
            :
              inc &i
              next

  $parse_attributes:
    $attributes empty_insert_order_table
    loop:
      if
        i > n
        -> i attributes
        :
          $chr text(i)
          if
            chr == '>'
            -> i attributes
            :
              if
                chr <= ' ':
                  inc &i
                  next
                :
                  parse_attribute_name !i $name
                  if
                    i > n || text(i) != '='
                    -> i undefined
                    :
                      inc &i
                      parse_attribute_value !i $value
                      !attributes(name) value
                      next

  $parse_attribute_name:
    $s i
    loop:
      if
        i > n:
          error PARSE_ERROR "Unexpected end of input" range(text s -1)
        :
          $chr text(i)
          if
            chr <= ' ' || chr == '=' || chr == '>'
            -> i range(text s i-1)
            :
              inc &i
              next

  $parse_attribute_value:
    loop:
      if
        i > n:
          error PARSE_ERROR "Unexpected end of input" range(text i -1)
        :
          $chr text(i)
          if
            chr == '@quot;':
              inc &i
              $s i
              loop:
                if
                  i > n:
                    error PARSE_ERROR "Unexpected end of input" range(text i -1)
                  :
                    !chr text(i)
                    if
                      chr == '@quot;'
                      -> i+1 range(text s i-1)
                      :
                        inc &i
                        next
            :
              case chr
                ' ':
                  inc &i
                  next
                '>':
                  error PARSE_ERROR "Expected attribute value" range(text i -1)
                :
                  $s i
                  inc &i
                  loop:
                    if
                      i > n
                      -> i range(text s i-1)
                      :
                        !chr text(i)
                        if
                          chr <= ' ' || chr == '>'
                          -> i range(text s i-1)
                          :
                            inc &i
                            next

  $parse_contents:
    $contents empty_list
    loop:
      if
        i > n
        -> i contents
        :
          $chr text(i)
          if
            chr == '<':
              if
                i+1 < n && text(i+1) == '/'
                -> i contents
                :
                  parse_element !i $element
                  push &contents element
                  next
            :
              parse_simple_text !i $simple_text
              push &contents simple_text
              next

  $parse_simple_text:
    $s i
    loop:
      if
        i > n
        -> i range(text s i-1)
        :
          $chr text(i)
          if
            chr == '<':
              -> i range(text s i-1)
            :
              inc &i
              next

[source]

$std::from_html: (text)
  !is_a_void_element
    hash_set
      "area"
      "base"
      "br"
      "col"
      "embed"
      "hr"
      "img"
      "input"
      "keygen"
      "link"
      "meta"
      "param"
      "source"
      "track"
      "wbr"
  parse_html &text $element
  if
    element.is_defined && text .matches. some(WHITESPACE)
    -> element
    :
      error PARSE_ERROR "Unexpected input" text

[documentation remark]

### Always copy examples contained in the symbol's description remark
    into the documentation

### Use only one of the following topics: HTML

# std::from_html (a function)

